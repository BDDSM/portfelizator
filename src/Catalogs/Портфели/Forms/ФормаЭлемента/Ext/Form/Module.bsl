
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивТикеров = ОбщегоНазначенияСервер.ПолучитьМассивТикеров();
	Элементы.СоставПортфеляТикер.СписокВыбора.ЗагрузитьЗначения(МассивТикеров);
	
КонецПроцедуры

&НаКлиенте
Процедура МетапортфельПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	
	ЭтоМетапортфель = Объект.Метапортфель;
	Элементы.СтраницаСоставПортфеля.Видимость = (НЕ ЭтоМетапортфель);
	Элементы.СтраницаСоставМетапортфеля.Видимость = ЭтоМетапортфель;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Функция УбратьОбрамляющиеКавычки(Стр)
	
	Если Лев(Стр, 1) = """" И Прав(Стр, 1) = """" Тогда
		Возврат Сред(Стр, 2, СтрДлина(Стр) - 2);
	КонецЕсли; 
	
	Возврат Стр;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьСоставПортфеляИзЦСВ(ИмяФайла)
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла);
	Стр = ЧтениеТекста.ПрочитатьСтроку();
	Пока Стр <> Неопределено Цикл
		Поз = Найти(Стр, ",");
		СтрТикер = Лев(Стр, Поз - 1);
		СтрДоля = Сред(Стр, Поз + 1);
		СтрТикер = УбратьОбрамляющиеКавычки(СтрТикер);
		НоваяСтрока = Объект.СоставПортфеля.Добавить(); 
		НоваяСтрока.Тикер = СокрЛП(СтрТикер);
		Попытка
			СтрДоля = УбратьОбрамляющиеКавычки(СтрДоля);
			СтрДоля = СокрЛП(СтрДоля);
			СтрДоля = СтрЗаменить(СтрДоля, ",", ".");
			СтрДоля = СтрЗаменить(СтрДоля, " ", "");
			Если Прав(СтрДоля, 1) = "%" Тогда
				СтрДоля = Лев(СтрДоля, СтрДлина(СтрДоля) - 1);
			КонецЕсли; 
			НоваяСтрока.Доля = Число(СтрДоля);
		Исключение
		КонецПопытки; 
		Стр = ЧтениеТекста.ПрочитатьСтроку();
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЦСВФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		ЗагрузитьСоставПортфеляИзЦСВ(ВыбранныеФайлы[0]);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзЦСВ(Команда)
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.Фильтр = "CSV-Файлы (*.csv)|*.csv|Все файлы (*.*)|*.*";
	ДополнительныеПараметры = Новый Структура; 
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборЦСВФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ВыборФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

